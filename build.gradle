
repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'

apply plugin: 'java'
apply plugin: 'idea'

group = 'com.github.briandilley'
version = '0.1-SNAPSHOT'
description = """
Java library for joining objects into a graph, JOin gRAPH
"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version:'1.3.RC2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version:'1.3.RC2'
    testCompile group: 'junit', name: 'junit', version:'4.10'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.5-rc1'
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}


if (project.hasProperty('release')) {

    subprojects {

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    }

    allprojects {
        apply plugin: 'signing'
        apply plugin: 'maven'

        // Signature of artifacts
        signing {
            sign configurations.archives
        }

        // OSSRH publication
        uploadArchives {
            repositories {
                mavenDeployer {
                    // POM signature
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    // Target repository
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUser, password: ossrhPassword)
                    }
                    pom.project {
                        name project.name
                        description project.description
                        packaging 'jar'
                        url 'https://github.com/briandilley/joraph'

                        scm {
                            connection 'scm:git:https://github.com/briandilley/joraph.git'
                            developerConnection 'scm:git:git@github.com:briandilley/joraph.git'
                            url 'https://github.com/briandilley/joraph.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'briandilley'
                                name = 'Brian Dilley'
                                email = 'briandilley@briandilley.com'
                            }
                        }
                    }
                }
            }
        }

    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}